task copyApplication(type: Sync) {
    def installDistTask = project(':java:faasit.dsl.ide').tasks.installDist
    dependsOn installDistTask
    from installDistTask.outputs
    into 'src/faasit'
}

clean {
    doLast {
        delete copyApplication.outputs
    }
}

pnpmInstall.dependsOn copyApplication

task installExtension(type: Exec, dependsOn: vscodeExtension) {
    if (System.properties['os.name'].toLowerCase().contains('windows')) {
        commandLine 'code.cmd'
    } else {
        commandLine 'code'
    }
    args '--install-extension', vscodeExtension.destPath
}

task startCode(type:Exec, dependsOn: installExtension) {
    if (System.properties['os.name'].toLowerCase().contains('windows')) {
        commandLine 'code.cmd'
    } else {
        commandLine 'code'
    }
    args "$rootProject.projectDir/demo/", '--new-window'
}

task publish(dependsOn: vscodeExtension, type: PnpmTask) {
    args = [ 'run', 'publish', '-p', System.getenv('ACCESS_TOKEN'), "--packagePath", "${project.buildDir}/vscode/vscode-faasit-${project.version}.vsix"]
    execOverrides {
        workingDir = projectDir
    }
}